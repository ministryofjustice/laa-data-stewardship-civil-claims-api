plugins {
    id 'org.openapi.generator' version '7.13.0'
    id 'uk.gov.laa.ccms.springboot.laa-ccms-spring-boot-gradle-plugin'
    id("org.springframework.boot") version "3.2.4"
    id("io.spring.dependency-management") version "1.1.4"
    id("java")
}

group = "uk.gov.justice.laa"
version = "1.0.0"
java.sourceCompatibility = JavaVersion.VERSION_21

repositories {
    mavenCentral()
}

//Overriding tomcat version coming from laa-ccms-spring-boot-gradle-plugin -> spring boot 3.5.3
// to avoid snyk warning that stops the build
//Remove the line below when the laa-ccms-spring-boot-gradle-plugin is upgraded
ext['tomcat.version'] = '10.1.43'

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
}

if (project.hasProperty("swaggerOnly") && project.property("swaggerOnly") == "true") {
    compileJava.enabled = false
    tasks.named("openApiGenerate").configure {
        enabled = false
    }
}

sourceSets.main.java.srcDirs += ['generated/src/main/java']

checkstyleMain.exclude "*"

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/claims-data/api/open-api-specification.yml".toString()
    outputDir = "$rootDir/claims-data/api/generated".toString()
    apiPackage = "uk.gov.justice.laa.dstew.payments.claimsdata.api"
    invokerPackage = "uk.gov.justice.laa.dstew.payments.claimsdata.invoker"
    modelPackage = "uk.gov.justice.laa.dstew.payments.claimsdata.model"
    configOptions = [
            generateBuilders    : "true",
            interfaceOnly       : "true", // This will only generate interfaces, not implementations
            serializableModel   : "true",
            skipDefaultInterface: "true",
            useJakartaEe        : "true",
            useSpringBoot3      : "true",
            useSwaggerUI        : "true",
            useTags             : "true",
    ]
}

import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

tasks.register('openApiGenerateSwaggerOnly', GenerateTask) {
    generatorName = "html2"
    inputSpec = "$rootDir/claims-data/api/open-api-specification.yml"
    outputDir = layout.buildDirectory.dir("generated/swagger-ui").get().asFile.toString()
    skipValidateSpec = false
    configOptions = [
            "hideDownloadButton": "true",
            "theme": "flattop"  // or another Swagger UI theme if you prefer
    ]
}

tasks.register("prepareSwaggerUI") {
    dependsOn "generateSwaggerUIOnly"
    doLast {
        copy {
            from(layout.buildDirectory.dir("generated/swagger-ui").get().asFile.toString())
            into("src/main/resources/static")
        }
    }
}

compileJava.dependsOn 'openApiGenerate'

clean {
    delete "$rootDir/laa-data-stewardship-civil-claims-api/generated"
}

// disable for overall project when running a build
tasks.named('bootJar') {
    enabled = false
}

tasks.named('bootRun') {
    enabled = false
}
